TITLE "Zeitbasis, Projekt SE2K, W.Panschow, Vers: V04, Stand 19.08.04" ;

PARAMETERS(Test_Ein);

FUNCTION freq_teiler (clk, cnt_en, sclr)
	WITH (Test, Teiler, Test_cnt)
	RETURNS (F_div);

SUBDESIGN zeitbasis
(
	clk					: INPUT;
	Ena_every_100ns		: OUTPUT;
	Ena_every_250ns		: OUTPUT;
	Ena_every_500ns		: OUTPUT;
	Ena_every_us		: OUTPUT;
	Ena_every_ms		: OUTPUT;
	Every_10ms			: OUTPUT;
	Every_20ms			: OUTPUT;
	Gap_Timer_Intr		: OUTPUT;
)

VARIABLE
	Ena_every_100ns_cnt		: freq_teiler	WITH (Test = 0, Teiler = 4, Test_cnt = 4);
	Ena_every_250ns_cnt		: freq_teiler	WITH (Test = 0, Teiler = 10, Test_cnt = 10);
	Ena_every_500ns_cnt		: freq_teiler	WITH (Test = 0, Teiler = 20, Test_cnt = 20);
	Ena_every_us_cnt		: freq_teiler	WITH (Test = 0, Teiler = 40, Test_cnt = 40);
	Ena_every_500us_cnt		: freq_teiler	WITH (Test = Test_Ein, Teiler = 500, Test_cnt = 6);
	Every_10ms_cnt			: freq_teiler	WITH (Test = Test_Ein, Teiler = 10, Test_cnt = 5);
	Every_20ms_cnt			: freq_teiler	WITH (Test = Test_Ein, Teiler = 20, Test_cnt = 7);
	Toggle_every_500us		: TFF;
	Ena_every_ms_ff			: DFF;
	Gap_Timer_Intr_ff		: DFF;
	
BEGIN
Ena_every_100ns_cnt.clk = clk;
Ena_every_100ns = Ena_every_100ns_cnt.f_div;
 	
Ena_every_250ns_cnt.clk = clk;
Ena_every_250ns = Ena_every_250ns_cnt.f_div;
 	
Ena_every_500ns_cnt.clk = clk;
Ena_every_500ns = Ena_every_500ns_cnt.f_div;
	
Ena_every_us_cnt.clk = clk;
Ena_every_us = Ena_every_us_cnt.f_div;

Ena_every_500us_cnt.clk		= clk;
Ena_every_500us_cnt.cnt_en	= Ena_every_us;
	
Every_10ms_cnt.clk		= clk;		
Every_10ms_cnt.cnt_en	= Ena_every_ms;
Every_10ms = Every_10ms_cnt.f_div;

Every_20ms_cnt.clk		= clk;		
Every_20ms_cnt.cnt_en	= Ena_every_ms;
Every_20ms = Every_20ms_cnt.f_div;

Toggle_every_500us.clk	= clk;
Toggle_every_500us.t	= Ena_every_500us_cnt.f_div;

Ena_every_ms_ff.clk	= clk;
Ena_every_ms_ff.d	= Ena_every_500us_cnt.f_div and not Toggle_every_500us;
Ena_every_ms = Ena_every_ms_ff.q;

-- Der Gap_Timer_Intr soll 500 us Versatz zum Ena_every_ms Signal haben	--

Gap_Timer_Intr_ff.clk	= clk;
Gap_Timer_Intr_ff.d		= Ena_every_500us_cnt.f_div and Toggle_every_500us;
Gap_Timer_Intr = Gap_Timer_Intr_ff.q;

END;
