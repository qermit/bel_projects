TITLE "IFACTL_b";

FUNCTION SRFFE (S, R, CLK, CLRN, PRN, ENA)	RETURNS (Q);
FUNCTION SRFF  (S, R, CLK, CLRN, PRN)		RETURNS (Q);
FUNCTION DFF      (D, CLK, CLRN, PRN)		RETURNS (Q);
FUNCTION DFFE     (D, CLK, CLRN, PRN, ENA)	RETURNS (Q);

FUNCTION lpm_ff (data[LPM_WIDTH-1..0], clock, enable, sclr, sset, sload, aclr, aset, aload)
   WITH (LPM_WIDTH, LPM_AVALUE, LPM_SVALUE, LPM_FFTYPE)
   RETURNS (q[LPM_WIDTH-1..0]);

FUNCTION lpm_compare (dataa[LPM_WIDTH-1..0], datab[LPM_WIDTH-1..0], clock, aclr)
   WITH (LPM_WIDTH, LPM_REPRESENTATION, LPM_PIPELINE, CHAIN_SIZE, ONE_INPUT_IS_CONSTANT)
   RETURNS (alb, aeb, agb, ageb, aneb, aleb);

FUNCTION lpm_mux (data[LPM_SIZE-1..0][LPM_WIDTH-1..0], sel[LPM_WIDTHS-1..0], clock, aclr) 
   WITH (LPM_WIDTH, LPM_SIZE, LPM_WIDTHS, LPM_PIPELINE)
   RETURNS (result[LPM_WIDTH-1..0]);


----- Funktions-Codes fuer allg. Interface-Karte (IFA) -----

CONSTANT C_RD_IRM			= H"C9";
CONSTANT C_RD_CNTRL			= H"CA";
CONSTANT C_RD_ID			= H"CC";
CONSTANT C_RD_vers			= H"CD";
CONSTANT C_RD_echo			= H"89";
CONSTANT C_RD_IF_Mode		= H"97";
CONSTANT C_rd_I2C			= H"98";

CONSTANT C_WR_SW1			= H"06";
CONSTANT C_WR_IRM			= H"12";
CONSTANT C_WR_ECHO			= H"13";
CONSTANT C_WR_IF_Mode		= H"60";
CONSTANT C_WR_I2C			= H"61";

CONSTANT C_Reset_CMD		= H"01";
CONSTANT C_res_vw_err		= H"7D";
CONSTANT C_res_broadcast	= H"7E";
CONSTANT C_set_broadcast	= H"7F";


SUBDESIGN IFACTL_b

    (
	CLK,					-- System-Clock  
	IFA_ADR[7..0],			-- IF-Karten-Adresse
    FC_Str,					-- Funktionscode-Strobe	 ______+-+_______
--  FC_Str_Piggy,			-- FC-Strobe-Piggy       ___+-------+____
	FC[7..0],				-- Funktionscode
	DI[15..0],				-- Daten-Sollwert Intern
	DIW[15..0],				-- Daten-Istwert
	STS[7..0],				-- 8 Bit Status von der VG-Leiste
	INRM[7..0],				-- 8 Bit Interrupt-Maske
	IFA_Ctrl[7..0],			-- IFA Internes Control-Status
	IFA_ID[7..0],			-- IFA Identifikations-Code
	IFA_Epld_Vers[7..0],	-- IFA Epld-Versions-Nummer
	I2C_Data[12..0],		-- Daten I2C-Bus 
	/JP_LE_SW,				-- Jumper: 0V => Latch-Enable-Sollwert
	/sel_sts_data,			-- 0V => Status-Bits werden von DIW[15..8] gelesen
	IF_Mode[15..0],			-- Reg. Interface-Karten-Mode;
	SCLR					-- syn. Clear
				:	INPUT;

	IFA_SD_Mux_Sel[3..0],	-- Selekt des Muliplexers für Status und Daten der IFA
	IFA_SD_Mux[15..0],		-- Ausgang des Muliplexers für Status und Daten der IFA
	VG_Data[15..0],			-- Daten-Ausgang im IFA-Mode zur VG-Leiste
	SEND_STR,				-- Send-Strobe zum Starten des 'EE-FlipFlops' für den Senders '6408'
	FC_EXT,					-- Strobe zur Anzeige eines Datenzugriffes (nur ext. Daten)
	IFA_FC_INT,				-- Funktionscodes für IFA
	WR_I2C,					-- Write Daten des I2C-Bus
	WR_IRM,					-- Write Interrupt-Maske
	Broad_EN,				-- Flag, für Broadcast-Enable
    Reset_CMD,				-- Reset Command (FC = 01) 
    res_vw_err				-- Reset VW-Error (in 'SerPar_a') 
				:	OUTPUT;
    )



VARIABLE

	v_ED   	  : LPM_FF WITH (LPM_WIDTH=16);
	v_VG_Data : LPM_FF WITH (LPM_WIDTH=16);
	v_VGD_en  : DFF;						-- Enable IFA-Daten zur VG-Leiste


	v_FC_INT    	: DFF; 
	v_FC_EXT    	: DFF; 
	v_SEND_STR   	: DFF;
	v_WR_IRM     	: DFF;
	v_res_vw_err	: DFF;
	v_reset_cmd		: DFF;
	v_WR_I2C     	: DFF;
	v_Broad_EN  	: SRFF;

	v_RD_IRM    	: DFF; 
	v_RD_CNTRL  	: DFF; 
	v_RD_ID     	: DFF; 
	v_RD_vers   	: DFF; 
	v_RD_echo    	: DFF;
	v_RD_STS     	: DFF;
	v_RD_IW_Data	: DFF;
	v_RD_I2C		: DFF;
	v_RD_IF_Mode	: DFF;

	v_IFA_SD_mux	: LPM_MUX WITH (LPM_WIDTH=16, LPM_SIZE=10, LPM_WIDTHS=4, LPM_PIPELINE = 1);
		-- LPM_WIDTH	=	Width of the data[][] and result[] ports. 
		-- LPM_SIZE		=	Number of input buses to the multiplexer. LPM_SIZE <= 2 ^ LPM_WIDTHS.
		-- LPM_WIDTHS	=	Width of the sel[] input port.
		
	v_mux_sel[3..0] 		:	NODE; -- Sel für Multiplexer

BEGIN

--+==========================================================================
--| 				dekodierte Funktionscode Strobes   
--+==========================================================================

--+--------------------------------------------------------------------------
--| v_IFA_FC_INT (dekodierte Funktionscodes der Interface-Karte 'IFA')   
--+--------------------------------------------------------------------------
v_FC_INT.(D, CLK, CLRN) =
	 ( (  (fc[] == C_RD_IRM) 	  # (fc[] == C_RD_CNTRL)	  # (fc[] == C_RD_ID)
		# (fc[] == C_RD_Vers)	  # (fc[] == C_RD_Echo)		  # (fc[] == C_RD_IF_Mode)
		# (fc[] == C_RD_I2C)

		# (fc[] == C_WR_SW1) 	  # (fc[] == C_WR_IRM)		  # (fc[] == C_WR_Echo)
		# (fc[] == C_WR_IF_Mode)  # (fc[] == C_WR_I2C)

		# (fc[] == C_res_vw_err)  # (fc[] == C_res_broadcast) # (fc[] == C_set_broadcast)), clk, NOT SCLR);

--+--------------------------------------------------------------------------
--| v_FC_Ext (Strobe zur Anzeige eines Datenzugriffes (nur ext. Daten))   
--+--------------------------------------------------------------------------
v_FC_EXT.(D, CLK, CLRN)  = ( NOT v_FC_INT & fc_str, clk, NOT SCLR);

--+--------------------------------------------------------------------------
--| FKT-Codes für Istwert-Daten Lesen    
--+--------------------------------------------------------------------------

v_RD_IW_Data .(D, CLK, CLRN) = (( (fc7 & NOT fc6) & NOT (	(fc[] == C_RD_Echo)
														 OR	(fc[] == C_RD_I2C)
													 	 OR	(fc[] == C_RD_IF_Mode)) %& fc_str% ), CLK, NOT SCLR);

--+--------------------------------------------------------------------------
--| FKT-Codes für Status Lesen    
--+--------------------------------------------------------------------------
v_RD_IRM     .(D, CLK, CLRN) = (((fc[] == C_RD_IRM  )    & fc_str), CLK, NOT SCLR);
v_RD_CNTRL   .(D, CLK, CLRN) = (((fc[] == C_RD_CNTRL)	 & fc_str), CLK, NOT SCLR);
v_RD_ID      .(D, CLK, CLRN) = (((fc[] == C_RD_ID   )    & fc_str), CLK, NOT SCLR);
v_RD_vers    .(D, CLK, CLRN) = (((fc[] == C_RD_vers )    & fc_str), CLK, NOT SCLR);

v_RD_STS     .(D, CLK, CLRN) = (( (fc7 & fc6) & NOT (	(fc[] == C_RD_IRM)
													 OR	(fc[] == C_RD_CNTRL)
													 OR	(fc[] == C_RD_ID )
													 OR	(fc[] == C_RD_vers )) & fc_str), CLK, NOT SCLR);

--+--------------------------------------------------------------------------
--| FKT-Codes für Echo-Daten Lesen    
--+--------------------------------------------------------------------------
v_RD_echo    .(D, CLK, CLRN) = (((fc[] == C_RD_echo )    & fc_str), CLK, NOT SCLR);

--+--------------------------------------------------------------------------
--| Send-Strobe zum Starten des 'EE-FlipFlops' für den Senders '6408' 
--+--------------------------------------------------------------------------
v_SEND_STR   .(D, CLK, CLRN) = ( fc7 AND fc_str, CLK, NOT SCLR);

--+--------------------------------------------------------------------------
--| FKT-Codes für Daten Schreiben    
--+--------------------------------------------------------------------------
v_WR_IRM     .(D, CLK, CLRN) = (((fc[] == C_WR_IRM  )   & fc_str), CLK, NOT SCLR);
v_res_vw_err .(D, CLK, CLRN) = (((fc[] == C_res_vw_err) & fc_str), CLK, NOT SCLR);
v_reset_cmd  .(D, CLK, CLRN) = (((fc[] == C_reset_cmd)  & fc_str), CLK, NOT SCLR);

--+--------------------------------------------------------------------------
--| FKT-Codes für I2C-Bus     
--+--------------------------------------------------------------------------
v_RD_I2C     .(D, CLK, CLRN) = (((fc[] == C_RD_I2C)      & fc_str), CLK, NOT SCLR);
v_WR_I2C     .(D, CLK, CLRN) = (((fc[] == C_WR_I2C)      & fc_str), CLK, NOT SCLR);

--+--------------------------------------------------------------------------
--| Interface-Karten-Mode 
--+--------------------------------------------------------------------------
v_RD_IF_Mode .(D, CLK, CLRN)             = (((fc[] == C_RD_IF_Mode)  & fc_str), CLK, NOT SCLR);

--+==========================================================================
--| Flag's    
--+==========================================================================
v_Broad_EN.(S, R, CLK, CLRN) = ( ((fc[] == C_res_broadcast) & fc_str)
								,((fc[] == C_set_broadcast) & fc_str),   clk, NOT SCLR);

--+==========================================================================
--| Echo-Daten-Register    
--+==========================================================================
v_ED	 .(data[], clock, enable, aclr) = (DI[], clk, ((fc[] == C_WR_ECHO ) & fc_str),  SCLR);

--+==========================================================================
--| IFA-Daten-Register zur VG-Leiste    
--+==========================================================================
v_VGD_en .(D, CLK, CLRN) = ( (((fc[] == C_WR_SW1) & fc_str & NOT /JP_LE_SW) OR /JP_LE_SW), clk , NOT SCLR);
v_VG_Data.(data[], clock, enable, aclr) = (DI[], clk, v_VGD_EN.q,  SCLR);

--+==========================================================================
--| Muliplexer für Status und Daten der IFA    
--+==========================================================================

v_IFA_SD_Mux.Data[0][15..8]  = STS[7..0]	    ; v_IFA_SD_Mux.Data[0][7..0]  = IFA_ADR[];	-- Status VG-Leiste
v_IFA_SD_Mux.Data[1][15..8]  = DIW[15..8]	    ; v_IFA_SD_Mux.Data[1][7..0]  = IFA_ADR[];	-- Status-Bits aus DIW[15..8]
v_IFA_SD_Mux.Data[2][15..8]  = INRM[7..0]	    ; v_IFA_SD_Mux.Data[2][7..0]  = GND; 		-- INR-Maske 
v_IFA_SD_Mux.Data[3][15..8]  = IFA_CTRL[]	    ; v_IFA_SD_Mux.Data[3][7..0]  = GND;		-- IFA-CTRL (intern)
v_IFA_SD_Mux.Data[4][15..8]  = IFA_ID[]	    	; v_IFA_SD_Mux.Data[4][7..0]  = GND;		-- IFA-ID Code 
v_IFA_SD_Mux.Data[5][15..8]  = IFA_Epld_Vers[]	; v_IFA_SD_Mux.Data[5][7..0]  = GND;		-- IFA-Epld-Version
v_IFA_SD_Mux.Data[6][15..0]  = DIW[15..0]     	;									   		-- Daten-Istwert
v_IFA_SD_Mux.Data[7][15..0]  = v_ED.q[15..0]  	;									   		-- Echo-Daten
v_IFA_SD_Mux.Data[8][15..13] = GND				; v_IFA_SD_Mux.Data[8][12..0] = I2C_Data[12..0]; -- INR-Maske 
v_IFA_SD_Mux.Data[9][15..0]  = IF_Mode[15..0]	;											-- IF-Mode Register

--+--------------------------------------------------------------------------
--| Selekt für Multiplexer     
--+--------------------------------------------------------------------------
v_IFA_SD_Mux.clock = clk;
v_IFA_SD_Mux.SEL[] = v_mux_sel[];

IF	v_RD_STS.q AND	   /SEL_STS_Data THEN v_mux_sel[] = 0; END IF; -- Multiplexer auf Status VG-Leiste
IF	v_RD_STS.q AND NOT /SEL_STS_Data THEN v_mux_sel[] = 1; END IF; -- Multiplexer auf Status aus DIW[15..0]
IF	v_RD_IRM.q						 THEN v_mux_sel[] = 2; END IF; -- Multiplexer auf INR-Maske
IF	v_RD_CNTRL.q					 THEN v_mux_sel[] = 3; END IF; -- Multiplexer auf IFA-CTRL (intern)
IF	v_RD_ID.q						 THEN v_mux_sel[] = 4; END IF; -- Multiplexer auf IFA-ID Code
IF	v_RD_vers.q						 THEN v_mux_sel[] = 5; END IF; -- Multiplexer auf IFA-Epld-Version
IF	v_RD_IW_Data.q					 THEN v_mux_sel[] = 6; END IF; -- Multiplexer auf Istert-Daten
IF	v_RD_echo.q						 THEN v_mux_sel[] = 7; END IF; -- Multiplexer auf Echo-Daten
IF	v_RD_I2C.q						 THEN v_mux_sel[] = 8; END IF; -- Multiplexer auf Daten I2C-Bus
IF	v_RD_IF_Mode.q					 THEN v_mux_sel[] = 9; END IF; -- Multiplexer auf Interface-Karten-Mode Register


--+##########################################################################
--|                            Ausgänge 
--+##########################################################################

	FC_EXT			=  v_FC_EXT			.q;
	IFA_FC_INT		=  v_FC_INT			.q;

	SEND_STR		=  v_SEND_STR   	.q;
	Res_vw_err		=  v_res_vw_err		.q;
	Reset_CMD		=  v_reset_cmd		.q;
	Broad_EN		=  NOT v_Broad_EN	.q;
	WR_I2C			=  v_WR_I2C     	.q;
	WR_IRM			=  v_WR_IRM     	.q;	-- Write Interrupt-Maske

	IFA_SD_Mux[]	=  v_IFA_SD_Mux.result[]; -- Ausgang des Muliplexers
	IFA_SD_Mux_Sel[]=  v_mux_sel[];			  -- Selekt des Muliplexers

	VG_Data[]		=  v_VG_Data.q[];         -- Daten-Ausgang im IFA-Mode zur VG-Leiste

END;


